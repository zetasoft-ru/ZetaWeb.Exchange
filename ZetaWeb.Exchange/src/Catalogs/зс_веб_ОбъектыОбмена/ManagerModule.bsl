#Область ПрограммныйИнтерфейс

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПроверитьОбъектыОбменаИЗапросы(ОписаниеОшибки = "") Экспорт
	
	ВсеВПорядке = Истина;
	
	//для объекта обмена без флага "ОбъектКонфигурации" должен быть хотя бы один запрос с видом конфигурации ЗетаВеб
	
	//для объекта обмена с флагом "ОбъектКонфигурации" должен быть хотя бы один запрос с видом конфигурации не ЗетаВеб
	
	//имя метаданных должно быть заполнено
	
	//полный запрос должен быть заполнен
	
	//запрос изменений должен быть заполнен
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидКонфигурацииЗетаВеб", Справочники.ЗетаWEBВидыКонфигураций.ЗетаWEB);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	зс_веб_ОбъектыОбмена.Ссылка КАК ОбъектОбмена,
	|	зс_веб_ОбъектыОбмена.ОбъектКонфигурации КАК ОбъектКонфигурации
	|ПОМЕСТИТЬ втОбъектыОбмена
	|ИЗ
	|	Справочник.зс_веб_ОбъектыОбмена КАК зс_веб_ОбъектыОбмена
	|ГДЕ
	|	НЕ зс_веб_ОбъектыОбмена.ЭтоГруппа
	|	И НЕ зс_веб_ОбъектыОбмена.НеУчаствуетВОбмене
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъектыОбмена.ОбъектОбмена КАК ОбъектОбмена,
	|	ВЫБОР
	|		КОГДА зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НЕ втОбъектыОбмена.ОбъектКонфигурации
	|					ТОГДА ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации, ЗНАЧЕНИЕ(Справочник.ЗетаWebВидыКонфигураций.ПустаяСсылка)) = &ВидКонфигурацииЗетаВеб
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕстьЗапросЗетаВеб,
	|	ВЫБОР
	|		КОГДА зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА втОбъектыОбмена.ОбъектКонфигурации
	|					ТОГДА ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации, ЗНАЧЕНИЕ(Справочник.ЗетаWebВидыКонфигураций.ПустаяСсылка)) <> &ВидКонфигурацииЗетаВеб
	|				ИНАЧЕ ИСТИНА
	|			КОНЕЦ
	|	КОНЕЦ КАК ЕстьЗапросПрикладнойКонфигурации,
	|	ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации, ЗНАЧЕНИЕ(Справочник.ЗетаWebВидыКонфигураций.ПустаяСсылка)) КАК ВидКонфигурации,
	|	ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ИмяМетаданных, """") <> """" КАК ЕстьИмяМетаданных,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ЗапросПолный, """") КАК СТРОКА(100))) <> """" КАК ЕстьЗапросПолный,
	|	(ВЫРАЗИТЬ(ЕСТЬNULL(зс_веб_ЗапросыОбъектовОбмена.ЗапросИзменения, """") КАК СТРОКА(100))) <> """" КАК ЕстьЗапросИзменения
	|ПОМЕСТИТЬ втОбъектыОбменаИЗапросы
	|ИЗ
	|	втОбъектыОбмена КАК втОбъектыОбмена
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.зс_веб_ЗапросыОбъектовОбмена КАК зс_веб_ЗапросыОбъектовОбмена
	|		ПО втОбъектыОбмена.ОбъектОбмена = зс_веб_ЗапросыОбъектовОбмена.ОбъектОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъектыОбменаИЗапросы.ОбъектОбмена КАК ОбъектОбмена,
	|	втОбъектыОбменаИЗапросы.ЕстьЗапросЗетаВеб КАК ЕстьЗапросЗетаВеб,
	|	втОбъектыОбменаИЗапросы.ЕстьЗапросПрикладнойКонфигурации КАК ЕстьЗапросПрикладнойКонфигурации,
	|	втОбъектыОбменаИЗапросы.ВидКонфигурации КАК ВидКонфигурации,
	|	втОбъектыОбменаИЗапросы.ЕстьИмяМетаданных КАК ЕстьИмяМетаданных,
	|	втОбъектыОбменаИЗапросы.ЕстьЗапросПолный КАК ЕстьЗапросПолный,
	|	втОбъектыОбменаИЗапросы.ЕстьЗапросИзменения КАК ЕстьЗапросИзменения
	|ИЗ
	|	втОбъектыОбменаИЗапросы КАК втОбъектыОбменаИЗапросы
	|ГДЕ
	|	НЕ(втОбъектыОбменаИЗапросы.ЕстьИмяМетаданных
	|				И втОбъектыОбменаИЗапросы.ЕстьЗапросПолный
	|				И втОбъектыОбменаИЗапросы.ЕстьЗапросИзменения)
	|ИТОГИ
	|	МАКСИМУМ(ЕстьЗапросЗетаВеб),
	|	МАКСИМУМ(ЕстьЗапросПрикладнойКонфигурации)
	|ПО
	|	ОбъектОбмена"
	;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаОбъектовОбмена = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Пока ВыборкаОбъектовОбмена.Следующий() Цикл
		
		ЗаголовокОшибки = "Объект обмена: " + Строка(ВыборкаОбъектовОбмена.ОбъектОбмена) + Символы.ПС;
		
		Если НЕ ВыборкаОбъектовОбмена.ЕстьЗапросЗетаВеб И ВыборкаОбъектовОбмена.ЕстьЗапросПрикладнойКонфигурации Тогда
			
			ВсеВПорядке = Ложь;
				
			Если НЕ ВыборкаОбъектовОбмена.ЕстьЗапросЗетаВеб Тогда
				
				ТекстОшибки = ЗаголовокОшибки + "Не задан запрос для конфигурации 'ЗетаWeb'";
				
				ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
				
			КонецЕсли;
			
			Если НЕ ВыборкаОбъектовОбмена.ЕстьЗапросПрикладнойКонфигурации Тогда
				
				ТекстОшибки = ЗаголовокОшибки + "Не задан запрос для прикладной конфигурации";
				
				ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
				
			КонецЕсли;
			
		КонецЕсли;
		
		Выборка = ВыборкаОбъектовОбмена.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ВсеВПорядке = Ложь;
			
			Если НЕ Выборка.ЕстьИмяМетаданных Тогда
				
				ТекстОшибки = ЗаголовокОшибки + "Не задано имя метаданных в регистре запросов";
				
				ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
				
			КонецЕсли;		
			
			Если НЕ Выборка.ЕстьЗапросПолный Тогда
				
				ТекстОшибки = ЗаголовокОшибки + "Не задан полный запрос в регистре запросов";
				
				ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
				
			КонецЕсли;		
			
			Если НЕ Выборка.ЕстьЗапросИзменения Тогда
				
				ТекстОшибки = ЗаголовокОшибки + "Не задан запрос изменений в регистре запросов";
				
				ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
				
			КонецЕсли;		
			
		КонецЦикла;
		
	КонецЦикла;
	
	Возврат ВсеВПорядке;

КонецФункции // ПроверитьОбъектыОбменаИЗапросы()

// <Описание функции>
//
// Параметры:
//  <Параметр1>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//  <Параметр2>  - <Тип.Вид> - <описание параметра>
//                 <продолжение описания параметра>
//
// Возвращаемое значение:
//   <Тип.Вид>   - <описание возвращаемого значения>
//
Функция ПроверитьОбъектыОбменаИПланыОбмена(МетаданныеПланОбмена, ВидКонфигурации, ОписаниеОшибки = "") Экспорт
	
	ВсеВПорядке = Истина;

	ИмяПланаОбмена = Перечисления.зс_веб_ИменаПлановОбмена[МетаданныеПланОбмена.Имя];
	
	СоставПлановОбмена =  зс_веб_ОбменДаннымиОбщий.ЗаполнитьСоставПланаОбмена(МетаданныеПланОбмена);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СоставПлановОбмена", СоставПлановОбмена);
	Запрос.УстановитьПараметр("ИмяПланаОбмена", ИмяПланаОбмена);
	Запрос.УстановитьПараметр("ВидКонфигурации", ВидКонфигурации);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СоставПлановОбмена.ИмяМетаданных КАК ИмяМетаданных,
	|	СоставПлановОбмена.ИмяПланаОбмена КАК ИмяПланаОбмена
	|ПОМЕСТИТЬ втСоставПлановОбмена
	|ИЗ
	|	&СоставПлановОбмена КАК СоставПлановОбмена
	|ГДЕ
	|	СоставПлановОбмена.ИмяПланаОбмена = &ИмяПланаОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зс_веб_ЗапросыОбъектовОбмена.ОбъектОбмена КАК ОбъектОбмена,
	|	зс_веб_ЗапросыОбъектовОбмена.ИмяМетаданных КАК ИмяМетаданных
	|ПОМЕСТИТЬ втЗапросыОбмена
	|ИЗ
	|	РегистрСведений.зс_веб_ЗапросыОбъектовОбмена КАК зс_веб_ЗапросыОбъектовОбмена
	|ГДЕ
	|	зс_веб_ЗапросыОбъектовОбмена.ВидКонфигурации = &ВидКонфигурации
	|	И зс_веб_ЗапросыОбъектовОбмена.ОбъектОбмена.ИмяПланаОбмена = &ИмяПланаОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	зс_веб_ОбъектыОбмена.Ссылка КАК ОбъектОбмена,
	|	ЕСТЬNULL(втЗапросыОбмена.ИмяМетаданных, """") КАК ИмяМетаданных
	|ПОМЕСТИТЬ втОбъектыОбменаСИменемМетаданных
	|ИЗ
	|	втЗапросыОбмена КАК втЗапросыОбмена
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.зс_веб_ОбъектыОбмена КАК зс_веб_ОбъектыОбмена
	|		ПО втЗапросыОбмена.ОбъектОбмена = зс_веб_ОбъектыОбмена.Ссылка
	|ГДЕ
	|	зс_веб_ОбъектыОбмена.ТипОбъекта <> ЗНАЧЕНИЕ(Перечисление.ЗетаWEBТипыОбъектовКонфигурации.ТЧ)
	|	И НЕ зс_веб_ОбъектыОбмена.НеУчаствуетВОбмене
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	втОбъектыОбменаСИменемМетаданных.ОбъектОбмена КАК ОбъектОбмена,
	|	ЕСТЬNULL(втОбъектыОбменаСИменемМетаданных.ИмяМетаданных, """") <> """" КАК ЕстьВРегистре,
	|	ЕСТЬNULL(втСоставПлановОбмена.ИмяМетаданных, """") <> """" КАК ЕстьВПланеОбмена,
	|	ЕСТЬNULL(втОбъектыОбменаСИменемМетаданных.ИмяМетаданных, """") КАК ИмяМетаданныхРегистр,
	|	ЕСТЬNULL(втСоставПлановОбмена.ИмяМетаданных, """") КАК ИмяМетаданныхПланОбмена
	|ИЗ
	|	втОбъектыОбменаСИменемМетаданных КАК втОбъектыОбменаСИменемМетаданных
	|		ПОЛНОЕ СОЕДИНЕНИЕ втСоставПлановОбмена КАК втСоставПлановОбмена
	|		ПО втОбъектыОбменаСИменемМетаданных.ИмяМетаданных = втСоставПлановОбмена.ИмяМетаданных
	|ГДЕ
	|	НЕ(ЕСТЬNULL(втОбъектыОбменаСИменемМетаданных.ИмяМетаданных, """") <> """"
	|				И ЕСТЬNULL(втСоставПлановОбмена.ИмяМетаданных, """") <> """")"
	;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		ВсеВПорядке = Ложь;
		
		ЗаголовокОшибки = "Вид конфигурации: " + Строка(ВидКонфигурации) + ". План обмена: " + МетаданныеПланОбмена.Имя + Символы.ПС;
		
		Если НЕ Выборка.ЕстьВРегистре Тогда
			
			ТекстОшибки = ЗаголовокОшибки + "Объект метаданных: " + Выборка.ИмяМетаданныхПланОбмена + " не найден (возможно, не заполно имя метаданных в регистре запросов)";
			
			ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
			
		КонецЕсли;
		
		Если НЕ Выборка.ЕстьВПланеОбмена Тогда
			
			ТекстОшибки = ЗаголовокОшибки + "Объект обмена: " + Строка(Выборка.ОбъектОбмена) + " с именем метаданных " + Строка(Выборка.ИмяМетаданныхРегистр) + " не найден в плане обмена";
			
			ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ВсеВПорядке;

КонецФункции // ПроверитьОбъектыОбменаИПланыОбмена()

#КонецОбласти

#Область ОбработчикиСобытий
//Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
//Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьОписаниеОшибки(ТекстОшибки, ОписаниеОшибки)
	
	Если НЕ ПустаяСтрока(ОписаниеОшибки) Тогда
		ОписаниеОшибки = ОписаниеОшибки + Символы.ПС + Символы.ПС;
	КонецЕсли;
	
	ОписаниеОшибки = ОписаниеОшибки + ТекстОшибки;
	
КонецПроцедуры

#КонецОбласти